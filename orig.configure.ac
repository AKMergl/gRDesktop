# grdesktop - gtk rdesktop frontend
# Copyright (C) 2002 Thorsten Sauter <tsauter@gmx.net>
#
# $Id: configure.ac,v 1.27 2004/03/30 13:59:11 tsauter Exp $
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#

AC_PREREQ([2.49])

dnl Global automake/autoconf files
dnl Shell commands prior to AC_INIT are ignored! :-(
AC_INIT

MAJOR_VERSION=0
MINOR_VERSION=24
MICRO_VERSION=rc7
VERSION=$MAJOR_VERSION.$MINOR_VERSION.$MICRO_VERSION

AM_INIT_AUTOMAKE(grdesktop, $VERSION)
AC_CONFIG_SRCDIR([src/main.c])
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

dnl Check default compiler options
AC_PROG_CC
AC_PROG_CC_STDC
AC_C_CONST

# Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T

# Checks for library functions.
AC_CHECK_FUNCS([memset setlocale])

dnl We need these programs to compile/install grdesktop
AC_PROG_INSTALL
AC_PROG_LN_S
AC_SEARCH_LIBS([strerror],[cposix])

dnl Check local system environment
AC_HEADER_STDC
AC_CHECK_HEADERS([string.h fcntl.h sys/files.h sys/params.h])
AC_CHECK_HEADERS([libintl.h locale.h stdlib.h unistd.h])

dnl GNU gettext functions
AC_PROG_INTLTOOL([0.23])
GETTEXT_PACKAGE=$PACKAGE
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE], ["${GETTEXT_PACKAGE}"], [gettext domain])

ALL_LINGUAS="de es fr hu"
AM_GLIB_GNU_GETTEXT

LOCALEDIR='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(LOCALEDIR)

dnl Defining license from file
LICENSETEXT="LICENSE.lic"
dnl The first param requires the $, which seems to be a bug in script generation.
dnl It will be translated to 'if test -r "<param>"'!
dnl AC_CHECK_FILE([$LICENSETEXT], [AC_SUBST_FILE(LICENSETEXT)],
dnl    [AC_MSG_ERROR([Can't find LICENSE file!])])
AC_SUBST_FILE([LICENSETEXT])

dnl Searching for GTK+ 2 includes/libs and export compiler flags
PKG_CHECK_MODULES(GTK2, gtk+-2.0, HAVE_GTK=yes, HAVE_GTK=no)
if test "$HAVE_GTK" = "no"; then
	AC_MSG_ERROR([cannot find GTK+ 2.0!])
fi
AC_SUBST(GTK2_CFLAGS)
AC_SUBST(GTK2_LIBS)

dnl Searching for GNOME 2 includes/libs and export compiler flags
PKG_CHECK_MODULES(GNOME2, libgnomeui-2.0, HAVE_GNOME2=yes, HAVE_GNOME2=no)
if test "$HAVE_GNOME2" = "no"; then
	AC_MSG_ERROR([cannot find GNOME2 2.0!])
fi
AC_SUBST(GNOME2_CFLAGS)
AC_SUBST(GNOME2_LIBS)

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
if test "$GCONFTOOL" = "no"; then
	AC_MSG_ERROR([cannot find gconf 2.0!])
fi
AM_GCONF_SOURCE_2

AC_CHECK_PROG(rdesktop, rdesktop, "yes", "no")
if test "$rdesktop" = "no"; then
	AC_MSG_WARN([*** rdesktop is required to use grdesktp! ***])
fi

AC_ARG_WITH(keymap-path, AS_HELP_STRING([--with-keymap-path=DIR],[search for rdesktop keymap files in DIR/]))
if test "$withval" = ""; then
	AC_MSG_WARN([--with-keymap-path called without argument - will use default])
	AC_SUBST(kbpath, "/usr/share/rdesktop/keymaps/")
else
	AC_SUBST(kbpath, $withval)
fi

dnl Generate the Makefiles
AC_CONFIG_FILES([Makefile
                 doc/Makefile
                 help/C/Makefile
                 help/Makefile
                 pixmaps/Makefile
                 src/Makefile
                 po/Makefile.in])
AC_OUTPUT

